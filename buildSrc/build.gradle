apply plugin: "java-gradle-plugin"
apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.8

sourceSets {
	main {
		java { srcDirs = [] }
		groovy { srcDirs += ['src/main/java'] }
	}
}

repositories {
	jcenter()
	gradlePluginPortal()
	mavenCentral()
	maven { url 'https://repo.spring.io/plugins-release/' }
}

gradlePlugin {
	plugins {
		trang {
			id = "trang"
			implementationClass = "trang.TrangPlugin"
		}
		locks {
			id = "locks"
			implementationClass = "lock.GlobalLockPlugin"
		}
		springModule {
			id = "org.springframework.security.spring-module"
			implementationClass = "org.springframework.security.convention.SpringModulePlugin"
		}
		springBom {
			id = "org.springframework.security.spring-bom"
			implementationClass = "org.springframework.security.convention.SpringBomPlugin"
		}
		docs {
			id = "org.springframework.security.spring-docs"
			implementationClass = "org.springframework.security.convention.DocsPlugin"
		}
	}
}
configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.group == "org.springframework.boot") {
			details.useVersion "2.4.2"
			details.because "Override Spring Boot Version Gretty uses"
		}

	}
}
configurations {
	implementation {
		exclude module: 'groovy-all'
	}
}

dependencies {
	compile 'com.thaiopensource:trang:20091111'
	compile 'net.sourceforge.saxon:saxon:9.1.0.8'
	implementation localGroovy()

	implementation 'com.github.ben-manes:gradle-versions-plugin:0.38.0'
	implementation 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1'
	implementation 'io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE'
	implementation 'io.spring.gradle:docbook-reference-plugin:0.3.1'
	implementation 'io.spring.gradle:propdeps-plugin:0.0.10.RELEASE'
	implementation 'io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.25'
	implementation 'io.spring.nohttp:nohttp-gradle:0.0.5.RELEASE'
	implementation 'org.asciidoctor:asciidoctor-gradle-jvm:3.1.0'
	implementation 'org.asciidoctor:asciidoctor-gradle-jvm-pdf:3.1.0'
	implementation 'org.hidetake:gradle-ssh-plugin:2.10.1'
	implementation 'org.jfrog.buildinfo:build-info-extractor-gradle:4.9.10'
	implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1'

	testImplementation 'junit:junit:4.12'
	testImplementation 'org.apache.commons:commons-io:1.3.2'
	testImplementation 'org.assertj:assertj-core:3.13.2'
	testImplementation 'org.mockito:mockito-core:3.0.0'
	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

test.onlyIf { !project.hasProperty("skipTest") }
