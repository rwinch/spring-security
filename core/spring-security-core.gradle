apply plugin: 'io.spring.convention.spring-module'

def includeProject = project(':spring-security-crypto')

configurations {
	tests
	published.extendsFrom tests, archives

	included
	compile.extendsFrom included
}

dependencies {
	compile 'aopalliance:aopalliance'
	compile "commons-logging:commons-logging"
	compile "org.springframework:spring-aop"
	compile "org.springframework:spring-beans"
	compile "org.springframework:spring-context"
	compile "org.springframework:spring-core"
	compile "org.springframework:spring-expression"

	included includeProject

	optional "net.sf.ehcache:ehcache"
	optional 'javax.annotation:jsr250-api'
	optional "org.aspectj:aspectjrt"
	optional "org.springframework:spring-jdbc"
	optional "org.springframework:spring-tx"
	optional "com.fasterxml.jackson.core:jackson-databind"

	testCompile "commons-collections:commons-collections"
	testCompile "org.springframework:spring-test"
	testCompile "org.slf4j:jcl-over-slf4j"
	testCompile powerMockDependencies
	testCompile "org.skyscreamer:jsonassert"

	testRuntime "org.hsqldb:hsqldb"
}

classes.doLast {
	copy {
		from includeProject.sourceSets.main.output
		into sourceSets.main.output.classesDir
	}
}

tasks.sourcesJar.from {includeProject.sourceSets.main.java}

configure(project.tasks.withType(Test)) {
	systemProperties['springSecurityVersion'] = version
	systemProperties['springVersion'] = project.dependencyManagement.managedVersions['org.springframework:spring-core']
}

task testJar(type: Jar) {
	classifier = 'tests'
	from sourceSets.test.output
}

artifacts {
	tests testJar
}

uploadPublished {
	// maven deployer configuration
}

install {
	configuration = configurations.published
}
