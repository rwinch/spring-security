plugins {
	id "nebula.integtest" version "7.0.9"
	id "java"
	id "war"
	id "org.gretty" version "3.0.2"
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/libs-snapshot" }
}

gretty {
	servletContainer = "tomcat85"
	contextPath = "/"
	fileLogEnabled = false
	integrationTestTask = 'integrationTest'
}

Task prepareAppServerForIntegrationTests = project.tasks.create('prepareAppServerForIntegrationTests') {
	group = 'Verification'
	description = 'Prepares the app server for integration tests'
	doFirst {
		project.gretty {
			httpPort = -1
		}
	}
}

project.tasks.withType(org.akhikhl.gretty.AppBeforeIntegrationTestTask).all { task ->
	task.dependsOn prepareAppServerForIntegrationTests
}

integrationTest.doFirst {
	def gretty = project.gretty
	String host = project.gretty.host ?: 'localhost'
	boolean isHttps = gretty.httpsEnabled
	Integer httpPort = integrationTest.systemProperties['gretty.httpPort']
	Integer httpsPort = integrationTest.systemProperties['gretty.httpsPort']
	int port = isHttps ? httpsPort : httpPort
	String contextPath = project.gretty.contextPath
	String httpBaseUrl = "http://${host}:${httpPort}${contextPath}"
	String httpsBaseUrl = "https://${host}:${httpsPort}${contextPath}"
	String baseUrl = isHttps ? httpsBaseUrl : httpBaseUrl
	integrationTest.systemProperty 'app.port', port
	integrationTest.systemProperty 'app.httpPort', httpPort
	integrationTest.systemProperty 'app.httpsPort', httpsPort
	integrationTest.systemProperty 'app.baseURI', baseUrl
	integrationTest.systemProperty 'app.httpBaseURI', httpBaseUrl
	integrationTest.systemProperty 'app.httpsBaseURI', httpsBaseUrl
}

dependencies {
	implementation platform("org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE")
	implementation platform("org.springframework.security:spring-security-bom:5.4.0.BUILD-SNAPSHOT")
	implementation "org.springframework.security:spring-security-config"
	implementation "org.springframework.security:spring-security-web"
	implementation "javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:latest.release"
	implementation "org.apache.taglibs:taglibs-standard-jstlel:latest.release"
	providedCompile "javax.servlet:javax.servlet-api:latest.release"
	providedCompile "javax.servlet.jsp:javax.servlet.jsp-api:latest.release"
	testImplementation "junit:junit:4.12"
	testImplementation "org.assertj:assertj-core:3.15.0"
	testImplementation "org.springframework:spring-test"
	testImplementation "org.springframework.security:spring-security-test"

	integTestImplementation "org.seleniumhq.selenium:htmlunit-driver:2.39.0"
}
