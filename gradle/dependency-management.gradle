if (!project.hasProperty("reactorVersion")) {
	ext.reactorVersion = "2020.0.+"
}

if (!project.hasProperty("springVersion")) {
	ext.springVersion = "5.3.+"
}

if (!project.hasProperty("springDataVersion")) {
	ext.springDataVersion = "Neumann-SR+"
}
if (!project.hasProperty("kotlinVersion")) {
	ext.kotlinVersion = "1.+"
}
if (!project.hasProperty("rsocketVersion")) {
	ext.rsocketVersion = "1.1.0"
}
if (!project.hasProperty("locksDisabled")) {
	dependencyLocking {
		lockAllConfigurations()
	}
}
ext.openSamlVersion = "3.+"

dependencies {
	management platform(project(":spring-security-dependencies"))
}

configurations {
	all {
		resolutionStrategy {
			componentSelection {
				all { ComponentSelection selection ->
					def candidate = selection.getCandidate()
					def version = candidate.getVersion().toLowerCase()

					if (version.contains("alpha") || version.contains("beta")) {
						selection.reject("Rejecting $selection with version $version as alpha/beta")
					}

					if (candidate.getModule().equals("jsr250-api") && version.equals("1.0-20050927.133100")) {
						selection.reject("Rejecting $selection with version $version as invalid version")
					}
				}
			}
		}
	}
}
