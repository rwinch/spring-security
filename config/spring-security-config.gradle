plugins {
	id 'org.springframework.security.spring-module'
	id 'kotlin'
	id "nebula.integtest"
	id 'trang'
}


java {
	registerFeature("optional") {
		usingSourceSet(sourceSets.main)
	}
}


import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

configurations.testImplementation.extendsFrom configurations.compileOnly

dependencies {
	// NB: Don't add other compile time dependencies to the config module as this breaks tooling
	compile project(':spring-security-core')
	compile 'org.springframework:spring-aop'
	compile 'org.springframework:spring-beans'
	compile 'org.springframework:spring-context'
	compile 'org.springframework:spring-core'

	optionalApi project(':spring-security-ldap')
	optionalApi project(':spring-security-messaging')
	optionalApi project(':spring-security-saml2-service-provider')
	optionalApi project(':spring-security-oauth2-client')
	optionalApi project(':spring-security-oauth2-jose')
	optionalApi project(':spring-security-oauth2-resource-server')
	optionalApi project(':spring-security-openid')
	optionalApi project(':spring-security-rsocket')
	optionalApi project(':spring-security-web')
	optionalApi 'io.projectreactor:reactor-core'
	optionalApi 'org.aspectj:aspectjweaver'
	optionalApi 'org.springframework:spring-jdbc'
	optionalApi 'org.springframework:spring-messaging'
	optionalApi 'org.springframework:spring-tx'
	optionalApi 'org.springframework:spring-webmvc'
	optionalApi'org.springframework:spring-web'
	optionalApi'org.springframework:spring-webflux'
	optionalApi'org.springframework:spring-websocket'
	optionalApi 'org.jetbrains.kotlin:kotlin-reflect'
	optionalApi 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

	compileOnly 'javax.servlet:javax.servlet-api'

	testCompile project(':spring-security-aspects')
	testCompile project(':spring-security-cas')
	testCompile project(':spring-security-test')
	testCompile project(path : ':spring-security-core', configuration : 'tests')
	testCompile project(path : ':spring-security-ldap', configuration : 'tests')
	testCompile project(path : ':spring-security-oauth2-client', configuration : 'tests')
	testCompile project(path : ':spring-security-oauth2-resource-server', configuration : 'tests')
	testCompile project(path : ':spring-security-saml2-service-provider', configuration : 'tests')
	testCompile project(path : ':spring-security-web', configuration : 'tests')
	testCompile "org.apache.directory.server:apacheds-core"
	testCompile "org.apache.directory.server:apacheds-core-entry"
	testCompile "org.apache.directory.server:apacheds-protocol-shared"
	testCompile "org.apache.directory.server:apacheds-protocol-ldap"
	testCompile "org.apache.directory.server:apacheds-server-jndi"
	testCompile 'org.apache.directory.shared:shared-ldap'
	testImplementation "org.powermock:powermock-core"
	testImplementation "org.powermock:powermock-api-support"
	testImplementation "org.powermock:powermock-module-junit4-common"
	testImplementation "org.powermock:powermock-module-junit4"
	testImplementation("org.powermock:powermock-api-mockito2") {
		exclude group: 'org.mockito', module: 'mockito-all'
	}
	testImplementation "org.powermock:powermock-reflect"
	testImplementation "junit:junit"
	testImplementation "org.mockito:mockito-core"
	testImplementation "org.springframework:spring-test"
	testImplementation "org.assertj:assertj-core"
	testImplementation 'com.squareup.okhttp3:mockwebserver'
	testImplementation 'ch.qos.logback:logback-classic'
	testImplementation 'io.projectreactor.netty:reactor-netty'
	testImplementation 'io.rsocket:rsocket-transport-netty'
	testImplementation 'javax.annotation:jsr250-api:1.0'
	testImplementation 'javax.xml.bind:jaxb-api'
	testImplementation 'ldapsdk:ldapsdk:4.1'
	testImplementation('net.sourceforge.htmlunit:htmlunit') {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	testImplementation 'org.eclipse.persistence:javax.persistence'
	testImplementation 'org.hibernate:hibernate-entitymanager'
	testImplementation 'org.hsqldb:hsqldb'
	testImplementation ('org.openid4java:openid4java-nodeps') {
		exclude group: 'com.google.code.guice', module: 'guice'
	}
	testImplementation('org.seleniumhq.selenium:htmlunit-driver') {
		exclude group: 'commons-logging', module: 'commons-logging'
	}
	testImplementation('org.seleniumhq.selenium:selenium-java') {
		exclude group: 'commons-logging', module: 'commons-logging'
		exclude group: 'io.netty', module: 'netty'
	}
	testImplementation 'org.slf4j:jcl-over-slf4j'
	testImplementation 'org.springframework.ldap:spring-ldap-core'
	testImplementation 'org.springframework:spring-expression'
	testImplementation 'org.springframework:spring-jdbc'
	testImplementation 'org.springframework:spring-orm'
	testImplementation 'org.springframework:spring-tx'
	testImplementation ('org.springframework.data:spring-data-jpa') {
		exclude group: 'org.aspectj', module: 'aspectjrt'
	}

	testRuntime 'org.hsqldb:hsqldb'
}


rncToXsd {
	rncDir = file('src/main/resources/org/springframework/security/config/')
	xsdDir = rncDir
	xslFile = new File(rncDir, 'spring-security.xsl')
}

tasks.withType(KotlinCompile).configureEach {
	kotlinOptions {
		jvmTarget = "1.8"
		freeCompilerArgs = ["-Xjsr305=strict"]
	}
}

build.dependsOn rncToXsd
